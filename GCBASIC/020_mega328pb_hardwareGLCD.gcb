// A demonstration program for GCBASIC
// ---------------------------------------------------------------------------------
// This program 
//
// @author  Evan Venn
// @licence GPL
// @version 1.0
// @date    17/04/2025
// ********************************************************************************

// Set microcontroller type and clock speed
#chip  MEGA328PB, 16
#option Explicit

// Define pin constants
    #Define LED     PORTB.5

#include <glcd.h>
    #DEFINE GLCD_TYPE GLCD_TYPE_ILI9488
    #DEFINE ILI9488_HARDWARESPI
    #DEFINE HWSPIMODE MasterUltraFast

    // ----- GLCD Ports for ILI9488
    #DEFINE GLCD_SCK      PortB.5  
    #DEFINE GLCD_DO       PortB.3
    #DEFINE GLCD_DI       PortB.4
    #DEFINE GLCD_RST      PortB.1
    #DEFINE GLCD_DC       PortB.0
    #DEFINE GLCD_CS       PortB.2

//*************************** Main Program ***************************************

    // Color component variables
        DIM Red AS BYTE
        DIM Green AS BYTE
        DIM Blue AS BYTE
        DIM MyCol AS LONG
        DIM Pixel AS LONG
        DIM YO AS BYTE       // Y-Offset for display
        DIM ccount as Word   // Counter for color testing
        DIM currentString as String * 14

    // String constant
    #DEFINE  SYSDEFAULTCONCATSTRING  24

    // Set display orientation
    GLCDROTATE Landscape

    Do

        // Test each primary color for this display
        For ccount = 1 to 19

            // Read color code
            ReadTable DisplayColor, ccount, MyCol

            // Print Color string
            GLCDPRINTLARGEFONT 0, 0, ConstructString ( ccount ), TFT_WHITE

            // Draw a yellow horizontal line for reference
            LINE (10,117,GLCDDeviceWidth-1,117,TFT_yellow)
            
            // Draw a small box with the current test color
            FILLEDBOX (GLCDDeviceWidth-9,110,GLCDDeviceWidth-1,116,MyCol)

            // Set Y-Offset for display
            YO=125 

            // Decode the pixel and display components
            Pixel = MyCol
            DecodeAndShow

            // Pause before testing next color
            Wait 1 s

        Next

    Loop
    End

    Sub DecodeAndShow
        Dim Pixel as Long

      // Display the original RGB888 value in hex
        GLCDPRINTLARGEFONT 10,YO,"0x"+LongToHex(Pixel)
        GLCDPRINTLARGEFONT 160,YO,"RGB666"

      // Extract Red component 
      Red = Pixel_U
        
      // Extract Green component 
      Green = Pixel_H
        
      // Extract Blue component 
      Blue = Pixel
        
      // Display binary representation of each color component
      GLCDPRINTLARGEFONT 10,YO+60,Right(ByteToBin(Red),6), TFT_RED
      GLCDPRINTLARGEFONT 90,YO+60,Right(ByteToBin(Green),6), TFT_GREEN
      GLCDPRINTLARGEFONT 172,YO+60,Right(ByteToBin(Blue),6), TFT_BLUE
    End Sub
    
    Table DisplayColor as Long
      TFT_BLACK     
      TFT_RED       
      TFT_GREEN     
      TFT_BLUE      
      TFT_WHITE     
      TFT_CYAN        
      TFT_DARKCYAN    
      TFT_DARKGREEN   
      TFT_DARKGREY    
      TFT_GREENYELLOW 
      TFT_LIGHTGREY   
      TFT_MAGENTA     
      TFT_MAROON      
      TFT_NAVY        
      TFT_OLIVE       
      TFT_ORANGE      
      TFT_PINK        
      TFT_PURPLE      
      TFT_YELLOW      
    End Table

    Function ConstructString ( ccount ) as String * 14

        Dim stringpointer as Word
        Dim stringpointercount as Byte
        Dim stringchar as Word
        ConstructString = ""
        For stringpointer = 0 to 14
            Readtable ColorStrings, ( 15 * ( ccount - 1 ) ) + stringpointer + 1, stringchar
            ConstructString = ConstructString + Chr(stringchar)
        Next

    End Function

    Table ColorStrings as Word

        "TFT_BLACK      "
        "TFT_RED        "
        "TFT_GREEN      "
        "TFT_BLUE       "
        "TFT_WHITE      "
        "TFT_CYAN       "
        "TFT_DARKCYAN   "
        "TFT_DARKGREEN  "
        "TFT_DARKGREY   "
        "TFT_GREENYELLOW"
        "TFT_LIGHTGREY  "
        "TFT_MAGENTA    "
        "TFT_MAROON     "
        "TFT_NAVY       "
        "TFT_OLIVE      "
        "TFT_ORANGE     "
        "TFT_PINK       "
        "TFT_PURPLE     "
        "TFT_YELLOW     "
    End Table


